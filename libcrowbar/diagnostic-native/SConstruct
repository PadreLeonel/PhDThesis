import os
from os.path import join
import subprocess
import sys
import platform

plat = platform.system().lower()
machine = platform.machine().lower()

if plat == 'windows':
  env = Environment(tools=['mingw'])
else:
  env = Environment()
  env['ENV']['TERM'] = os.environ['TERM']

vars = Variables('.scons.conf')
vars.Add('PATH', '', '')
vars.Add('LIBPATH', '', '')
vars.Add('CPPPATH', '', '')
vars.Add('COMPILER', '', '')

vars.Add('prefix', '', '#/target/install/')
vars.Add('build_dir', '', '#/target/build/')

vars.Add('boost_system', '', 'boost_system')

vars.Add('debug', '', False)
vars.Update(env)

# Platform
if plat == 'darwin':
  env['PLATFORM'] = plat
elif plat == 'windows':
  if machine == 'amd64':
    env['PLATFORM'] = 'win32-x86-64'
  else:
    env['PLATFORM'] = 'win32-x86'
elif plat == 'linux' and machine == 'i686':
    env['PLATFORM'] = 'linux-x86'
else:
  env['PLATFORM'] = (plat + "_" + platform.machine()).replace("_", "-").lower()

# Install/Build Dirs
for i in ['prefix', 'build_dir']:
    env[i]  = env.Dir(env[i]).abspath
for i in ['include', 'lib', 'bin']:
    env['%s_dir' % i] = join(env['prefix'], i, env['PLATFORM'])

# Paths
for i in ['PATH', 'LIBPATH', 'CPPPATH']:
    if not isinstance(env[i], list):
      env[i] = [env[i]]
env['PATH'].extend([env['bin_dir'],'/usr/local/bin', '/bin', '/usr/bin'])
env['LIBPATH'].extend([env['lib_dir']])
env['LIBPATH'].extend(['/usr/local/lib/'])
env['CPPPATH'].extend([env['include_dir']])
env['CPPPATH'].extend(['/usr/local/include/'])
env['ENV']['PATH'] = env['PATH']
env['ENV']['LD_LIBRARY_PATH'] = env['LIBPATH']

if env['COMPILER'] != '':
  env['CXX'] = env['COMPILER']


# Build Flags
if(env['debug']):
    env['CCFLAGS'] = "-g -O0"
else:
    env['CCFLAGS'] = "-O3 -DNDEBUG"
env['CCFLAGS'] += " -std=c++11  -Wall -pedantic"


# Link Flags
link_flags = ["-Wl,-rpath,'$$ORIGIN/../../lib/%s'" % env['PLATFORM']]

libs = [env['boost_system'],
        "mpfr",
        "gmp",]

if plat == 'windows':
  link_flags.extend(["-static-libgcc -static-libstdc++", "-static -lpthread"])
  libs.append("ws2_32")
else:
  libs.append("pthread")

env.Append(LINKFLAGS = "   ".join(link_flags))
env.Append(LIBS=libs)

Export('env')
SConscript('SConscript', variant_dir=env['build_dir'])
