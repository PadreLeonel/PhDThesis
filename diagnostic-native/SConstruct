import os
from os.path import join
import subprocess
import sys
import platform

env = Environment()
env['ENV']['TERM'] = os.environ['TERM']

vars = Variables('.scons.conf')
vars.Add('PATH', '', '')
vars.Add('LIBPATH', '', '')
vars.Add('CPPPATH', '', '')

vars.Add('prefix', '', '#/target/install/')
vars.Add('build_dir', '', '#/target/build/')

vars.Add('debug', '', False)
vars.Add('colors', '', True)
vars.Update(env)


if env['colors']:
    try:
        from colorizer import colorizer
        colorizer().colorize(env)
    except ImportError:
        pass

# Platform
plat = platform.system().lower()
if plat == 'darwin':
  env['PLATFORM'] = plat
else:
  env['PLATFORM'] = (plat + "_" + platform.machine()).replace("_", "-").lower()
print env['PLATFORM']

# Install/Build Dirs
for i in ['prefix', 'build_dir']:
    env[i]  = env.Dir(env[i]).abspath
for i in ['include', 'lib', 'bin']:
    env['%s_dir' % i] = join(env['prefix'], i, env['PLATFORM'])

# Paths
for i in ['PATH', 'LIBPATH', 'CPPPATH']:
    if not isinstance(env[i], list):
      env[i] = [env[i]]
env['PATH'].extend([env['bin_dir'],'/usr/local/bin', '/bin', '/usr/bin'])
env['LIBPATH'].extend([env['lib_dir']])
env['CPPPATH'].extend([env['include_dir']])
env['ENV']['PATH'] = env['PATH']
env['ENV']['LD_LIBRARY_PATH'] = env['LIBPATH']


# Build Flags
if(env['debug']):
    env['CCFLAGS'] = "-g -O0"
else:
    env['CCFLAGS'] = "-O3 -DNDEBUG"
env['CCFLAGS'] += " -std=c++11  -Wall -pedantic"


# Link Flags
link_flags = ["-Wl,-rpath,'$$ORIGIN/../../lib/%s'" % env['PLATFORM'],
              "-lboost_filesystem",
              "-lboost_system",
              "-lboost_date_time",
              "-lgmp",
              "-lmpfr",]

if env['PLATFORM'] == 'darwin':
  link_flags.append("-lboost_thread-mt")
else:
  link_flags.append("-lboost_thread")

env.Append(LINKFLAGS = "   ".join(link_flags))



Export('env')
SConscript('SConscript', variant_dir=env['build_dir'])
